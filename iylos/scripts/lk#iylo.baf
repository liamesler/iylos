// -------------------------------------
// Initial Meeting
// -------------------------------------
IF
    See(Player1)
    NumTimesTalkedTo(0)
    Global("LK#IylosJoinDialogue","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("LK#IylosJoinDialogue","GLOBAL",60)
    SetGlobal("LK#IylosJoinDialogue","GLOBAL",1)
END

IF
    RealGlobalTimerExpired("LK#IylosJoinDialogue","GLOBAL")
    See(Player1)
    NumTimesTalkedTo(0)
    Global("LK#IylosJoinDialogue","GLOBAL",1)
THEN
  RESPONSE #100
    PlaySong(0)
    PlaySound("lk_theme")
    Dialogue(Player1)
END

// -------------------------------------
// Balthazar Scene
// -------------------------------------
IF
    InParty("Iylos")
    Global("LK#IylosDialogueFadeNow","GLOBAL",1)
    !InMyArea("BALTH")
THEN
  RESPONSE #100
    IncrementGlobal("LK#IylosDialogueFadeNow","GLOBAL",1)
END

IF
    InParty("Iylos")
    Global("LK#IylosDialogueFadeNow","GLOBAL",2)
    !InMyArea("BALTH")
THEN
  RESPONSE #100
    MoveToObject(Player1)
    StartDialogue("LK#IYLJ","Player1")
END

// -------------------------------------
// Miscellaneous Changes
// -------------------------------------
// Swapping letter of Iylos likes the PC
IF
    GlobalGT("LK#IylosHappiness","GLOBAL",3)
    Global("LK#IylosLetterSwapped","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("LK#IylosLetterSwapped","GLOBAL",1)
    TakeItemReplace("lk#ilet2","lk#ilet1","Iylos")
END


 // Soundset changes for if Iylos likes the PC
IF
    GlobalGT("LK#IylosHappiness","GLOBAL",4)
    !Global("LK#IylosSoundsetHappy","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("LK#IylosSoundsetHappy","GLOBAL",1)
    SetGlobal("LK#IylosSoundsetUnhappy","GLOBAL",0)
    SetPlayerSound(Myself,~I'm sure I will do well, <CHARNAME>. A wise decision.~ [LK#I44],LEADER)
    SetPlayerSound(Myself,~Cease scuffing your boots on the floor and get a move on - the war will not wait, even for such as you.~ [LK#I45],BORED)
    SetPlayerSound(Myself,~Was there something you needed to ask me, or do you *enjoy* poking me?~ [LK#I46],SELECT_COMMON3)
    SetPlayerSound(Myself,~I am a leaf on the wind; watch me soar.~ [LK#I47],SELECT_COMMON5)
    SetPlayerSound(Myself,~Are virtues such as 'patience' and 'politeness' lost on you, Bhaalspawn?~ [LK#I48],SELECT_COMMON6)
    SetPlayerSound(Myself,~If you wish it, <CHARNAME> - I will do what I can.~ [LK#I49],SELECT_ACTION2)
    SetPlayerSound(Myself,~Please, <CHARNAME>, just tell me what to do and cease bothering me.~ [LK#I50],SELECT_ACTION4)
    SetPlayerSound(Myself,~Can't you do it yourself? (sigh)~ [LK#I51],SELECT_ACTION6)
    SetPlayerSound(Myself,~(Sarcastic) Of course, I see what you're trying to do - make me go mad. Of course. It's all part of your diabolical plot to make me turn evil, isn't it? I feel the madness creeping already...~ [LK#I52],SELECT_RARE2)
END


 // Soundset changes for if Iylos doesn't like the PC
IF
    GlobalLT("LK#IylosHappiness","GLOBAL",5)
    Global("LK#IylosSoundsetHappy","GLOBAL",1)
    !GLOBAL("LK#IylosSoundesetUnhappy","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("LK#IylosSoundsetHappy","GLOBAL",0)
    SetPlayerSound(Myself,~I'm sure I will do a better job with this group than you... a wise decision.~ [LK#I6],LEADER)
    SetPlayerSound(Myself,~Surely you have better things to do than stand around idly taking in the scenery, <CHARNAME>? The war will not wait for you- but I suppose the more blood shed the better, right? Pfah.~ [LK#I8],BORED)
    SetPlayerSound(Myself,~I don't need this...~ [LK#I23],SELECT_COMMON3)
    SetPlayerSound(Myself,~Perhaps you should ask someone else - I'm hardly feeling inclined to talk.~ [LK#I25],SELECT_COMMON5)
    SetPlayerSound(Myself,~What part of 'leave me alone' don't you understand?!~ [LK#I26],SELECT_COMMON6)
    SetPlayerSound(Myself,~Since I have no other choice, I will obey.~ [LK#I28],SELECT_ACTION2)
    SetPlayerSound(Myself,~Leave me alone!!~ [LK#I30],SELECT_ACTION4)
    SetPlayerSound(Myself,~<CHARNAME>, you are a fool if you cannot do so yourself. Not that I didn't already know that...~ [LK#I32],SELECT_ACTION6)
    SetPlayerSound(Myself,~I am not your pet *dog*, <CHARNAME>- I only do what you ask in the hopes that it will prove your guilt.~ [LK#I35],SELECT_RARE2)
END

// -------------------------------------
//  PC Dialogue (2)
// -------------------------------------
IF
    InParty(Myself)
    Global("LK#IylosFirstTimerSet","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("LK#IylosFirstTimerSet","GLOBAL",1)
    RealSetGlobalTimer("LK#IylosTalksTimer","GLOBAL",IYLOS_TIMER)
END

IF
    RealGlobalTimerExpired("LK#IylosTalksTimer","GLOBAL")
    Global("LK#IylosFirstTimerSet","GLOBAL",1)
    !AreaType(DUNGEON)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
THEN   
  RESPONSE #100
    SetGlobal("LK#IylosFirstTimerSet","GLOBAL",2)
    SetGlobal("LK#IylosL_Talks","GLOBAL",1)
END

IF
    RealGlobalTimerExpired("LK#IylosTalksTimer","GLOBAL")
    !AreaType(DUNGEON)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
    Global("LK#IylosL_Talks","GLOBAL",2)
THEN
  RESPONSE #100
    IncrementGlobal("LK#IylosL_Talks","GLOBAL",1)
END

IF
    RealGlobalTimerExpired("LK#IylosTalksTimer","GLOBAL")
    !AreaType(DUNGEON)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
    OR(2)
      Global("LK#IylosL_Talks","GLOBAL",1)
      Global("LK#IylosL_Talks","GLOBAL",3)
THEN
  RESPONSE #100
    PlaySong(0)
    PlaySound("lk_them2") 
    MoveToObject(Player1)
    StartDialogueNoSet(Player1)
END


// -------------------------------------
//  Reputation Dialogues (2)
// -------------------------------------
IF
    ReputationGT([PC],15)
    See(Player1)
    InParty(Myself)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    Global("LK#IylosReputationGoodTalk","GLOBAL",0)
    CombatCounter(0)
    !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("LK#IylosReputationGoodTalk","GLOBAL",1)
END

IF
    ReputationLT([PC],6)
    See(Player1)
    InParty(Myself)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    Global("LK#IylosReputationBadTalk","GLOBAL",0)
    CombatCounter(0)
    !See([ENEMY])
THEN
  RESPONSE #100
    SetGlobal("LK#IylosReputationBadTalk","GLOBAL",1)
END

IF
    OR(2)
      Global("LK#IylosReputationGoodTalk","GLOBAL",1)
      Global("LK#IylosReputationBadTalk","GLOBAL",1)
    See(Player1)
    InParty(Myself)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
THEN
  RESPONSE #100
    PlaySong(0)
    PlaySound("lk_them2") 
    MoveToObject(Player1)
    StartDialogueNoSet(Player1)
END


// -------------------------------------
//  Plot Dialogues (4)
// -------------------------------------
// Iylos' floating dialogue: What Next
IF
    Global("LK#IylosWhatCheck","GLOBAL",0)
    OR(2)
      Dead("Abazigal1")
      Dead("Sendai")
    !AreaType(DUNGEON)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
    Global("LK#IylosWhatNextDialogue","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("LK#IylosWhatCheck","GLOBAL",1)
    RealSetGlobalTimer("LK#IylosWhatNextTimer","GLOBAL",360)
END

IF
    Global("LK#IylosWhatCheck","GLOBAL",1)
    GlobalTimerExpired("LK#IylosWhatNextTimer","GLOBAL")
    OR(2)
      Dead("Abazigal1")
      Dead("Sendai")
    !AreaType(DUNGEON)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
    Global("LK#IylosWhatNextDialogue","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("LK#IylosWhatNextDialogue","GLOBAL",1)
END

IF
    GlobalTimerExpired("LK#IylosWhatNextTimer","GLOBAL")
    !AreaType(DUNGEON)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    See(Player1)
    CombatCounter(0)
    !See([ENEMY])
    Global("LK#IylosWhatNextDialogue","GLOBAL",1)
THEN
  RESPONSE #100
    PlaySong(0)
    PlaySound("lk_theme") 
    MoveToObject(Player1)
    StartDialogueNoSet(Player1)
END

// Balthazar Dead
IF 
    CombatCounter(0)
    !See([ENEMY])
    See(Player1)
    AreaCheck("AR5505")
    Dead("Balth")
    InParty(Myself)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    Global("LK#IylosBalthazarDead","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("LK#IylosBalthazarDead","GLOBAL",1)
END

IF
    CombatCounter(0)
    See(Player1)
    AreaCheck("AR5505")
    Dead("Balth")
    InParty(Myself)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    Global("LK#IylosBalthazarDead","GLOBAL",1)
THEN
  RESPONSE #100
    PlaySong(0)
    PlaySound("lk_them2")
    StartDialogueNoSet(Player1)
END

// -------------------------------------
//  Scenery Interjections (3)
// -------------------------------------
 // Amkethran
IF
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    !Dead("Iylos")
    AreaCheck("AR5500") 
    Global("LK#IylosAmkethran","GLOBAL",0)
    Global("LK#IylosAmkethranCheck","GLOBAL",0)
    !Global("LK#IylosTalksLessOften","GLOBAL",1)
THEN
  RESPONSE #100
    RealSetGlobalTimer("LK#IylosAmkethranTimer","GLOBAL",120)
    SetGlobal("LK#IylosAmkethranCheck","GLOBAL",1)
END
    
IF 
    GlobalTimerExpired("LK#IylosAmkethranTimer","GLOBAL")
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    !Dead("Iylos")
    AreaCheck("AR5500")
    Global("LK#IylosAmkethran","GLOBAL",0)
    Global("LK#IylosAmkethranCheck","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("LK#IylosAmkethran","GLOBAL",1)
END

IF
    GlobalTimerExpired("LK#IylosAmkethranTimer","GLOBAL")
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    !Dead("Iylos")
    AreaCheck("AR5500")
    Global("LK#IylosAmkethran","GLOBAL",1)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

 // Leila Banter
IF
    CombatCounter(0)
    !See([ENEMY])
    InMyArea("AMwenc01") 
    Range("AMwenc01",30) 
    InParty("Iylos") 
    Global("LK#IylosLeila","GLOBAL",0) 
    !StateCheck("Iylos",CD_STATE_NOTVALID)
THEN
  RESPONSE #100
    SetGlobal("LK#IylosLeila","GLOBAL",1)
END

IF
    CombatCounter(0)
    !See([ENEMY])
    InMyArea("AMwenc01") 
    Range("AMwenc01",30) 
    InParty("Iylos") 
    Global("LK#IylosLeila","GLOBAL",1)
    !StateCheck("Iylos",CD_STATE_NOTVALID)
THEN
  RESPONSE #100
    MoveToObject("AMwenc01")
    StartDialogueNoSet("AMwenc01")
END

 // Watcher's Keep 1
IF
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    !Dead("Iylos")
    AreaCheck("AR3000") 
    Global("LK#IylosWatchers1","GLOBAL",0)
    Global("LK#IylosWatchers1Check","GLOBAL",0)
    !Global("LK#IylosTalksLessOften","GLOBAL",1)
THEN
  RESPONSE #100
    RealSetGlobalTimer("LK#IylosWatchers1Timer","GLOBAL",60)
    SetGlobal("LK#IylosWatchers1Check","GLOBAL",1)
END

IF
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    !Dead("Iylos")
    AreaCheck("AR3000") 
    Global("LK#IylosWatchers1","GLOBAL",0)
    Global("LK#IylosWatchers1Check","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("LK#IylosWatchers1","GLOBAL",1)
END
    
IF 
    GlobalTimerExpired("LK#IylosWatchers1Timer","GLOBAL")
    InParty("Iylos")
    !Dead("Iylos")
    AreaCheck("AR3000")
    Global("LK#IylosWatchers1","GLOBAL",1)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END

 // Watcher's Keep 6
IF
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    !Dead("Iylos")
    !InMyArea("shugod01") 
    AreaCheck("AR3000")
    Global("LK#IylosWatchers1","GLOBAL",2)
    Global("LK#IylosWatchers6","GLOBAL",0) 
    Global("LK#IylosWatchers6Check","GLOBAL",0)
    !Global("LK#IylosTalksLessOften","GLOBAL",1)
THEN
  RESPONSE #100
    RealSetGlobalTimer("LK#IylosWatchers6Timer","GLOBAL",60)
    SetGlobal("LK#IylosWatchers6Check","GLOBAL",1)
END

IF
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    !Dead("Iylos")
    !InMyArea("shugod01")
    Global("LK#IylosWatchers6","GLOBAL",0)
    Global("LK#IylosWatchers6Check","GLOBAL",1)
    AreaCheck("AR3000")
THEN
  RESPONSE #100
    SetGlobal("LK#IylosWatchers6","GLOBAL",1)
END
    
IF 
    !StateCheck("Iylos",CD_STATE_NOTVALID)
    InParty("Iylos")
    !Dead("Iylos")
    AreaCheck("AR3000")
    Global("LK#IylosWatchers6","GLOBAL",1)
    GlobalTimerExpired("LK#IylosWatchers6Timer","GLOBAL")
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END